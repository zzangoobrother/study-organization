## 멀티 모듈 설정

멀티모듈은 처음 만들때 프로젝트를 root 라고 한다. root는 gradle은 설정 방식에 따라 서브 모듈에서 공통으로 사용할 gradle을 설정합니다.

root 모듈 gradle 설정
````java
buildscript {
    dependencies {
        classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.10")
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.10'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

// 모든 프로젝트에 적용.
allprojects {
    group = ''
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }
}

// 서브 프로젝트 전체에 적용.
subprojects {
    apply plugin: 'java'
    // 위에서 불러온 plugin 을 적용 시켰으므로 버전을 명시하지
    // 않아도 자식 프로젝트에서 2.7.10 버전을 받게된다.
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    ext {
        set('springCloudVersion', "2021.0.5")
    }

    // 공통적으로 사용하는 dependencies
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // spring docs (swagger)
        implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
        implementation 'org.springdoc:springdoc-openapi-security:1.7.0'

        runtimeOnly 'com.h2database:h2'
        runtimeOnly 'com.mysql:mysql-connector-j'

        // https://mvnrepository.com/artifact/com.amazonaws/aws-java-sdk-s3
        implementation group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.12.416'

        // https://mvnrepository.com/artifact/com.googlecode.json-simple/json-simple
        implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'

        implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.3'

        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

// 서브 프로젝트 개별 설정
// 이곳에서 lombok 같은 dependency 를 추가 할 수 있으나
// 해당 모듈 내에서도 추가 할 수 있기때문에 프로젝트간의 관계만 명시하도록 하자.
project(':module-api') {
    dependencies {
        implementation project(':module-core')
    }
}
project(':module-admin') {
    dependencies {
        implementation project(':module-core')
    }
}
project(':module-batch') {
    dependencies {
        implementation project(':module-core')
    }
}
project(':module-core') {
    dependencies {

    }
}
````

root 에서 필요없는 것들을 삭제합니다.
멀티모듈은 각 모듈에서 서버가 실행됩니다.
